plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
}

apply plugin: "java"
apply plugin: "application"

group = "org.kiva"
version = "1.0-SNAPSHOT"
sourceCompatibility = '11'

application {
    mainClass.set("org.kiva.fingerprint_desktop_tool.Templatize")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("info.picocli:picocli:4.5.2")
    implementation("com.machinezoo.sourceafis:sourceafis:3.11.0")
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.40.0"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}